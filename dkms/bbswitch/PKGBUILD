pkgname=bbswitch
_pkgver=0.8
pkgver=0.8.k6.1.161
pkgrel=1
pkgdesc="Bbswitch drivers for linux xanmod"
arch=("$CARCH")
url="https://github.com/Bumblebee-Project/bbswitch"
license=('GPL')
options=('!strip')
makedepends=(linux-xanmod-headers "bbswitch-dkms=${_pkgver}")
source=('kernel_signing_key.pub')
sha256sums=('71b5dddd64b9dbfb9abf6286adf17d137a6c3d260ee014e59aecbc0904f662e0')

prepare() {
	cp -a "/usr/src/bbswitch-${_pkgver}" bbswitch
}

pkgver() {
	local _kernver=$(</usr/src/linux-[0-9].*xanmod*/version)
	printf "${_pkgver}.k${_kernver%-*}"
}

build() {
	local _kdir="/usr/src/linux-[0-9].*xanmod*"
	make KDIR=${_kdir} -C bbswitch
	sign_mod "$(<${_kdir}/version)" bbswitch/*.ko || true
}

package() {
	local _kernver=$(</usr/src/linux-[0-9].*xanmod*/version)
	depends=("linux-xanmod=${_kernver%-*}")
	install -Dt "${pkgdir}/usr/lib/modules/${_kernver}/extramodules" -m644 bbswitch/*.ko
	find "${pkgdir}" -name '*.ko' -exec xz -T1 {} +

	echo "options bbswitch load_state=0 unload_state=1" | install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgbase}.conf"
	echo "bbswitch" | install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modules-load.d/${pkgbase}.conf"
}

sign_mod() {
	local KVER="$1"; shift
	local kernel_config="/usr/lib/modules/$KVER/build/.config"
	local sign_file="/usr/lib/modules/$KVER/build/scripts/sign-file"
	local sign_hash=$(grep "^CONFIG_MODULE_SIG_HASH=" "${kernel_config}" | cut -f2 -d= | sed 's/"//g')
	local mok_signing_key="${startdir}/kernel_signing_key"
	local mok_certificate="${srcdir}/kernel_signing_key.pub"

	options=('strip') tidy_strip
	for i in $@; do
		msg2 "Signing module: $(basename $i)"
		"$sign_file" "$sign_hash" "$mok_signing_key" "$mok_certificate" "$i"
	done
}
